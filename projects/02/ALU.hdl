// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Process X inputs
    Mux16(a=x, b[0..15]=false, sel=zx, out=rzx);        // select X or zero
    Not16(in=rzx, out=rnzx);                            // negate X
    Mux16(a=rzx, b=rnzx, sel=nx, out=rx);               // select X or Not(X)

    // Process Y inputs
    Mux16(a=y, b[0..15]=false, sel=zy, out=rzy);        // select X or zero
    Not16(in=rzy, out=rnzy);                            // negate X
    Mux16(a=rzy, b=rnzy, sel=ny, out=ry);               // select X or Not(X)

    // Compute the output given rX and rY
    Add16(a=rx, b=ry, out=xaddy);                       // x + y
    And16(a=rx, b=ry, out=xandy);                       // x & y
    Mux16(a=xandy, b=xaddy, sel=f, out=rf);             // select & or +
    Not16(in=rf, out=rnf);                              // Not(F)
    Mux16(a=rf, b=rnf, sel=no, out=rOut);               // select F or Not(F)
    Or16(a=rOut, b[0..15]=false, out=out);              // Copy rOut to output

    // Compute output comparisons
    IsZero16(in=rOut, out=zr);
    IsNegative16(in=rOut, out=ng);
}