// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    // The register to hold the counter in.
    // Always accepts a new load (even if it is a feedback load).
    Register(in=mResult, load=true, out=regOut, out=out);

    // The result of the next increment
    Inc16(in=regOut, out=nextInc);

    // Choose whether to inc, load, or reset
    Mux16(a=regOut, b=nextInc, sel=inc, out=m1);
    Mux16(a=m1, b=in, sel=load, out=m2);
    Mux16(a=m2, b=false, sel=reset, out=mResult);
}
